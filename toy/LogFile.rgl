# 综合演示
<set:formula>:sincurve
<set:am_method_default>:<pass_up_black=20>
<set:tx_method_default>:<w2w=1>
<set:bb_method_default>:<replace=0>

<set:BGM>:BGM1
<set:speech_speed>:500
<background><replace=30>:bg1

[张安翔]:^本视频是DanDDXuanX 编写的#TRPG-Replay-Generator的基本演示工程。{SE1;*3.5}
[KP]:^项目链接：#https://github.com/DanDDXuanX/TRPG-Replay-Generator{SE1;5}
<set:speech_speed>:300
<hitpoint>:(张安翔,10,10,9)

[张安翔]:^本工具基于python3 和 pygame2.0，在windows10系统上开发。#演示使用的媒体素材来自网络，侵删。{SE1;5}
[张安翔]:那么，演示开始。{SE1;5}

<hitpoint>:(张安翔,10,9,5)

<dice>:(测试姓名1 力量,100,75,3),(测试姓名2 sancheck,100,60,25),(测试姓名3 图书馆,100,75,85),(测试姓名4 侦察,100,60,100)
<dice>:(1d8,8,NA,4),(1d4,4,NA,4)
<hitpoint>:(短,4,4,1)
<hitpoint>:(用来测试的一极长文本,4,1,4)

<background><cross=60>:bg2
<set:speech_speed>:250
<set:am_method_default>:<replace=0>
<set:tx_method_default>:<all=0>
<set:am_dur_default>:10
<set:tx_dur_default>:7

<set:BGM>:stop

[张安翔]:在对话行里建立最基本的发言小节，播放基本音效。{SE1;5}
[KP]<black>:使用切换效果修饰符，指定切换模式为渐隐。{SE1;5}

<hitpoint>:(张安翔,10,5,7)

[张安翔]<black=30>:在切换效果修饰符中，指定切换时间为30。{SE1;5}
[KP]<black>:使用文本效果修饰符，指定文字显示模式为逐字显示。<w2w>{SE1;5}
[张安翔]<black>:在文本效果修饰符中，指定单位时间为2。<w2w=2>{SE1;5}

<hitpoint>:(张安翔,10,7,4)

[张安翔,KP.double]<black>:在角色框里指定多位角色，实现多人同框。<w2w=2>{SE1;5}
[KP.double,张安翔]<black>:置于首位的角色为主要发言人，其余角色设置为半透明。<w2w=2>{SE1;5}

<set:BGM>:BGM1

[旁白]<black>:没有立绘的文本框同样以角色的形式在角色表里定义，可以作为旁白或者骰子使用。<w2w=2>

<hitpoint>:(张安翔,10,4,9)

<set:BGM>:stop

<background><black=60>:bg1

[张安翔(60),KP.double]<replace=0>:调整角色的透明度参数，手动设置立绘透明度。<w2w=5>{SE1;5}
[张安翔.scared,KP.double]:指明角色的subtype，展示差分立绘。<w2w=5>{SE1;5}
[张安翔,KP.double]:^在发言文本中以井号作为换行符；#设置手动换行模式；#并逐行显示内容。<l2l=5>{SE1;5}

[旁白]<black>:———————演示结束——————

<hitpoint>:(张安翔,10,9,0)
